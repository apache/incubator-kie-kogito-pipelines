import org.jenkinsci.plugins.workflow.libs.Library
@Library('jenkins-pipeline-shared-libraries')_

// Deploy jobs
RUNTIMES_DEPLOY = 'kogito-runtimes-deploy'
OPTAPLANNER_DEPLOY = 'optaplanner-deploy'
APPS_DEPLOY = 'kogito-apps-deploy'
EXAMPLES_DEPLOY = 'kogito-examples-deploy'
IMAGES_DEPLOY = 'kogito-images-deploy'
OPERATOR_DEPLOY = 'kogito-operator-deploy'

// Promote jobs
IMAGES_PROMOTE = 'kogito-images-promote'
OPERATOR_PROMOTE = 'kogito-operator-promote'

// Map of executed jobs
// See https://javadoc.jenkins.io/plugin/workflow-support/org/jenkinsci/plugins/workflow/support/steps/build/RunWrapper.html
// for more options on built job entity
JOBS = [:]

FAILED_STAGES=[:]
UNSTABLE_STAGES=[:]

// Should be multibranch pipeline
pipeline {
    agent {
        label 'kie-rhel7 && !master'
    }

    triggers {
        cron ('H 0 * * *')
    }

    options {
        buildDiscarder logRotator(artifactDaysToKeepStr: '', artifactNumToKeepStr: '', daysToKeepStr: '', numToKeepStr: '30')
        timeout(time: 720, unit: 'MINUTES')
    }

    parameters {
        string(name: 'BUILD_BRANCH_NAME', defaultValue: '', description: 'Which branch to build? Set if you are not on a multibranch pipeline.')
        string(name: 'GIT_AUTHOR', defaultValue: 'kiegroup', description: 'Which Git author repository ?')

        string(name: 'ARTIFACTS_REPOSITORY', defaultValue: '', description: 'Provide a repository for artifacts to be deployed to')
        string(name: 'MAVEN_SETTINGS_CONFIG_FILE_ID', defaultValue: 'kogito_release_settings', description: 'Maven settings file to use for Maven config/deployment')

        booleanParam(name: 'SKIP_TESTS', defaultValue: false, description: 'Skip all tests')

        booleanParam(name: 'SKIP_ARTIFACTS', defaultValue: false, description: 'To skip Artifacts (runtimes, examples, optaplanner) Deployment')
        booleanParam(name: 'SKIP_IMAGES', defaultValue: false, description: 'To skip Images Deployment')
        booleanParam(name: 'SKIP_OPERATOR', defaultValue: false, description: 'To skip Operator Deployment')

        booleanParam(name: 'USE_TEMP_OPENSHIFT_REGISTRY', defaultValue: false, description: 'If enabled, use Openshift registry to push temporary images')
    }

    environment {
        KOGITO_CI_EMAIL_TO = credentials('KOGITO_CI_EMAIL_TO')

        // We use quay as temporary registry for testing between the jobs instead of Openshift, due to https://issues.redhat.com/browse/KOGITO-2219
        IMAGE_REGISTRY_CREDENTIALS = 'nightly_kogito'
        IMAGE_REGISTRY = 'quay.io'
        IMAGE_NAMESPACE = 'kiegroup'
        IMAGE_NAME_SUFFIX = 'nightly'
        TEMP_TAG = "temp-${getBuildBranch()}-tests"
        
        // Use branch name in nightly tag as we may have parallel master and release branch builds
        NIGHTLY_TAG = """${getBuildBranch()}-${sh(
                returnStdout: true,
                script: 'date -u "+%Y-%m-%d"'
            ).trim()}"""

        BRANCH_FOR_LATEST='master'

        // Credentials for PR creation
        BOT_AUTHOR='bsig-gh-bot'
        BOT_AUTHOR_CREDS_ID='bsig-gh-bot' // username/password creds
    }

    stages {
        stage('Initialize') {
            steps {
                script {
                    echo "temporary tag is ${env.TEMP_TAG}"
                    echo "nightly tag is ${env.NIGHTLY_TAG}"

                    currentBuild.displayName = env.NIGHTLY_TAG
                }
            }
        }

        stage('Build & Deploy Kogito Runtimes') {
            when {
                expression { return isArtifactsDeploy() }
            }
            steps {
                script {
                    echo "Call ${RUNTIMES_DEPLOY} job"
                    def buildParams = getDefaultBuildParams()
                    addSkipTestsParam(buildParams)
                    addMavenSettingsParam(buildParams)
                    addArtifactsRepositoriesParam(buildParams)
                    
                    // images and operator deploy testing will use older working artifacts if that one fails
                    buildJob(RUNTIMES_DEPLOY, buildParams, false)
                }
            }
            post {
                failure {
                    addFailedStage(RUNTIMES_DEPLOY)
                }
            }
        }

        stage('Build & Deploy OptaPlanner') {
            when {
                expression { return isArtifactsDeploy() }
            }
            steps {
                script {
                    echo "Call ${OPTAPLANNER_DEPLOY} job"

                    String optaplannerBranch = getBuildBranch()
                    String [] buildBranchSplit = optaplannerBranch.split("\\.")
                    if(buildBranchSplit.length == 3) {
                        /* The OptaPlanner major version is shifted by 7 from the Kogito major version:
                           Kogito 1.x.y -> OptaPlanner 8.x.y. */
                           
                        int majorVersionShift = 7
                        Integer optaplannerMajorVersion = Integer.parseInt(buildBranchSplit[0]) + majorVersionShift
                        optaplannerBranch = "${optaplannerMajorVersion}.${buildBranchSplit[1]}.${buildBranchSplit[2]}"
                    }

                    def buildParams = getDefaultBuildParams(optaplannerBranch)
                    addSkipTestsParam(buildParams)
                    addMavenSettingsParam(buildParams)
                    addArtifactsRepositoriesParam(buildParams)
                    def quickstartsBranch = optaplannerBranch == 'master' ? 'development' : optaplannerBranch
                    addStringParam(buildParams, 'QUICKSTARTS_BUILD_BRANCH_NAME', quickstartsBranch)
                    buildJob(OPTAPLANNER_DEPLOY, buildParams, false)
                }
            }
            post {
                failure {
                    addFailedStage(OPTAPLANNER_DEPLOY)
                }
            }
        }

        stage('Build & Deploy Kogito Apps') {
            when {
                expression { return isArtifactsDeploy() }
            }
            steps {
                script {
                    echo "Call ${APPS_DEPLOY} job"
                    def buildParams = getDefaultBuildParams()
                    addSkipTestsParam(buildParams)
                    addMavenSettingsParam(buildParams)
                    addArtifactsRepositoriesParam(buildParams)
                    
                    // images and operator deploy testing will use older working artifacts if that one fails
                    buildJob(APPS_DEPLOY, buildParams, false)
                }
            }
            post {
                failure {
                    addFailedStage(APPS_DEPLOY)
                }
            }
        }

        stage('Build & Deploy Kogito Examples') {
            when {
                expression { return isArtifactsDeploy() }
            }
            steps {
                script {
                    echo "Call ${EXAMPLES_DEPLOY} job"
                    def buildParams = getDefaultBuildParams()
                    addSkipTestsParam(buildParams)
                    addMavenSettingsParam(buildParams)
                    addArtifactsRepositoriesParam(buildParams)
                    
                    // images and operator deploy testing will use older working artifacts if that one fails
                    buildJob(EXAMPLES_DEPLOY, buildParams, false)
                }
            }
            post {
                failure {
                    addFailedStage(EXAMPLES_DEPLOY)
                }
            }
        }

        stage('Build & Deploy Images') {
            when {
                expression { return isImagesDeploy() }
            }
            steps {
                script {
                    echo "Call ${IMAGES_DEPLOY} job"

                    def buildParams = getDefaultBuildParams()
                    addSkipTestsParam(buildParams)
                    addImageBuildParams(buildParams, env.TEMP_TAG, '', true, false)
                    addExamplesParam(buildParams)
                    addCloudRepositoriesParam(buildParams)

                    // Ask to create the PR with changes occuring in this
                    addDeployGitParams(buildParams)
                    addBooleanParam(buildParams, 'CREATE_CHANGES_PR', true)

                    buildJob(IMAGES_DEPLOY, buildParams, false)
                }
            }
            post {
                failure {
                    addFailedStage(IMAGES_DEPLOY)
                }
            }
        }

        stage('Build & Deploy Operator') {
            when {
                expression { return isOperatorDeploy() }
            }
            steps {
                script {
                    echo "Call ${OPERATOR_DEPLOY} job"
                    def buildParams = getDefaultBuildParams()
                    addSkipTestsParam(buildParams)
                    addImageBuildParams(buildParams, env.TEMP_TAG, '', true, false)

                    // For BDD tests
                    addBooleanParam(buildParams, 'KOGITO_IMAGES_IN_OPENSHIFT_REGISTRY', params.USE_TEMP_OPENSHIFT_REGISTRY)
                    addStringParam(buildParams, 'KOGITO_IMAGES_REGISTRY', env.IMAGE_REGISTRY)
                    addStringParam(buildParams, 'KOGITO_IMAGES_NAMESPACE', env.IMAGE_NAMESPACE)
                    addStringParam(buildParams, 'KOGITO_IMAGES_NAME_SUFFIX', env.IMAGE_NAME_SUFFIX)
                    addStringParam(buildParams, 'KOGITO_IMAGES_TAG', env.TEMP_TAG)
                    addExamplesParam(buildParams)
                    addCloudRepositoriesParam(buildParams)

                    buildJob(OPERATOR_DEPLOY, buildParams, false)
                }
            }
            post {
                failure {
                    addFailedStage(OPERATOR_DEPLOY)
                }
            }
        }

        stage('Promote images') {
            when {
                expression { return isImagesPromote() }
            }
            steps {
                script {
                    echo "Call ${IMAGES_PROMOTE} job"

                    def buildParams = getDefaultBuildParams()
                    addImageBuildParams(buildParams, env.TEMP_TAG, 'BASE', true, false)
                    addImageBuildParams(buildParams, env.NIGHTLY_TAG, 'PROMOTE', true, true)
                    addBooleanParam(buildParams, 'DEPLOY_WITH_LATEST_TAG', isDeployImagesLatestTag())

                    buildJob(IMAGES_PROMOTE, buildParams)
                }
            }
            post {
                failure {
                    addFailedStage(IMAGES_PROMOTE)
                }
            }
        }

        stage('Promote operator image') {
            when {
                expression { return isOperatorPromote() }
            }
            steps {
                script {
                    echo "Call ${OPERATOR_PROMOTE} job"

                    def buildParams = getDefaultBuildParams()
                    addImageBuildParams(buildParams, env.TEMP_TAG, 'BASE', true, false)
                    addImageBuildParams(buildParams, env.NIGHTLY_TAG, 'PROMOTE', true, true)
                    addBooleanParam(buildParams, 'DEPLOY_WITH_LATEST_TAG', isDeployImagesLatestTag())

                    buildJob(OPERATOR_PROMOTE, buildParams)
                }
            }
            post {
                failure {
                    addFailedStage(OPERATOR_PROMOTE)
                }
            }
        }
    }
    post {
        unsuccessful {
            sendPipelineErrorNotification()
        }
    }
}

def buildJob(String jobName, List buildParams, boolean propagate=true){
    echo "Build ${jobName} with params ${buildParams} and propagate = ${propagate}"

    def job = build(job: "../${jobName}", wait: true, parameters: buildParams, propagate: propagate)
    JOBS[jobName] = job

    // Set Unstable if we don't propagate and job failed
    if(!propagate && !isJobSucceeded(jobName)){
        addUnstableStage(jobName)
        unstable("Job ${jobName} finished with result ${job.getResult()}")
    }
    return job
}

def getJob(String jobName){
    return JOBS[jobName]
}

String getJobUrl(String jobName){
    echo "getJobUrl for ${jobName}"
    def job = getJob(jobName)
    return job ? job.getAbsoluteUrl() : ''
}

boolean isJobSucceeded(String jobName){
    def job = getJob(jobName)
    return job ? job.getResult() == 'SUCCESS' : false
}

void addFailedStage(String jobName = ''){
    FAILED_STAGES.put("${STAGE_NAME}", jobName)
}
void addUnstableStage(String jobName = ''){
    UNSTABLE_STAGES.put("${STAGE_NAME}", jobName)
}

void sendPipelineErrorNotification(){
    String bodyMsg = "Kogito nightly job #${BUILD_NUMBER} was: ${currentBuild.currentResult}"
    
    paramsStr = ''
    if(params.SKIP_TESTS) {
        paramsStr += '\n- Tests skipped'
    }
    if(params.SKIP_ARTIFACTS) {
        paramsStr += '\n- Artifacts skipped'
    }
    if(params.SKIP_IMAGES) {
        paramsStr += '\n- Images skipped'
    }
    if(params.SKIP_OPERATOR) {
        paramsStr += '\n- Operator skipped'
    }
    bodyMsg += paramsStr ? "\n\nConfiguration:${paramsStr}" : '\n'

    if(FAILED_STAGES.size() > 0){
        bodyMsg += "\nFailed stages: \n- "
        bodyMsg += FAILED_STAGES.collect{ "${it.key} => ${getJobUrl(it.value)}" }.join("\n- ")
    }
    bodyMsg += "\n"
    if(UNSTABLE_STAGES.size() > 0){
        bodyMsg += "\nUnstable stages: \n- "
        bodyMsg += UNSTABLE_STAGES.collect{ "${it.key} => ${getJobUrl(it.value)}" }.join("\n- ")
    }
    bodyMsg += "\n"
    bodyMsg += "\nPlease look here: ${BUILD_URL}"
    emailext body: bodyMsg, subject: "[${getBuildBranch()}][d] Full Pipeline",
                to: env.KOGITO_CI_EMAIL_TO
}

List getDefaultBuildParams(String buildBranchName = ''){
    buildBranchName = buildBranchName ?: getBuildBranch()
    List params = []
    addStringParam(params, 'DISPLAY_NAME', env.NIGHTLY_TAG)
    addStringParam(params, 'BUILD_BRANCH_NAME', buildBranchName)
    addStringParam(params, 'GIT_AUTHOR', getGitAuthor())

    return params
}

void addSkipTestsParam(buildParams){
    addBooleanParam(buildParams, 'SKIP_TESTS', params.SKIP_TESTS)
}

void addMavenSettingsParam(buildParams){
    addStringParam(buildParams, 'MAVEN_SETTINGS_CONFIG_FILE_ID', params.MAVEN_SETTINGS_CONFIG_FILE_ID)
}

void addArtifactsRepositoriesParam(buildParams){
    addStringParam(buildParams, 'MAVEN_DEPENDENCIES_REPOSITORY', getArtifactsRepositoryParam())
    addStringParam(buildParams, 'MAVEN_DEPLOY_REPOSITORY', getArtifactsRepositoryParam())
}

void addCloudRepositoriesParam(buildParams){
    addStringParam(buildParams, 'MAVEN_ARTIFACT_REPOSITORY', getArtifactsRepositoryParam())
}

void addExamplesParam(buildParams){
    addStringParam(buildParams, 'EXAMPLES_URI', "https://github.com/${getGitAuthor()}/kogito-examples")
    addStringParam(buildParams, 'EXAMPLES_REF', getBuildBranch())
}

void addDeployGitParams(buildParams){
    addStringParam(buildParams, 'GIT_AUTHOR_BOT', env.BOT_AUTHOR)
    addStringParam(buildParams, 'BOT_CREDENTIALS_ID', env.BOT_AUTHOR_CREDS_ID)
}

void addImageBuildParams(List buildParams, String tag, String paramsPrefix = '', boolean isRegistryIncluded = false, boolean isFinalImage = false){
    if (!isFinalImage && params.USE_TEMP_OPENSHIFT_REGISTRY) {
        addBooleanParam(buildParams, constructKey(paramsPrefix, 'IMAGE_USE_OPENSHIFT_REGISTRY'), true)
    } else if(isRegistryIncluded) {
        addStringParam(buildParams, constructKey(paramsPrefix, 'IMAGE_REGISTRY_CREDENTIALS'), env.IMAGE_REGISTRY_CREDENTIALS)
        addStringParam(buildParams, constructKey(paramsPrefix, 'IMAGE_REGISTRY'), env.IMAGE_REGISTRY)
        addStringParam(buildParams, constructKey(paramsPrefix, 'IMAGE_NAMESPACE'), env.IMAGE_NAMESPACE)
    }
    addStringParam(buildParams, constructKey(paramsPrefix, 'IMAGE_NAME_SUFFIX'), env.IMAGE_NAME_SUFFIX)
    addStringParam(buildParams, constructKey(paramsPrefix, 'IMAGE_TAG'), tag)
}

void addStringParam(List params, String key, String value){
    params.add(string(name: key, value: value))
}

void addBooleanParam(List params, String key, boolean value){
    params.add(booleanParam(name: key, value: value))
}

String constructKey(String prefix, String paramId){
    return prefix ? "${prefix}_${paramId}" : paramId
}

String getBuildBranch(){
    return params.BUILD_BRANCH_NAME ?: env.BRANCH_NAME
}

String getGitAuthor(){
    return params.GIT_AUTHOR
}

boolean isDeployImagesLatestTag(){
    return getBuildBranch() == env.BRANCH_FOR_LATEST
}

boolean isArtifactsDeploy(){
    return !params.SKIP_ARTIFACTS
}

boolean isImagesDeploy(){
    return !params.SKIP_IMAGES
}

boolean isOperatorDeploy(){
    return !params.SKIP_OPERATOR
}

boolean isImagesPromote(){
    return isImagesDeploy() && isJobSucceeded(IMAGES_DEPLOY)
}

boolean isOperatorPromote(){
    return isOperatorDeploy() && isJobSucceeded(OPERATOR_DEPLOY)
}

String getArtifactsRepositoryParam(){
    return params.ARTIFACTS_REPOSITORY
}
