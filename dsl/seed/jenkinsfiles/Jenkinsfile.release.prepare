import org.jenkinsci.plugins.workflow.libs.Library
import java.util.regex.Pattern

@Library('jenkins-pipeline-shared-libraries')_

seedConfig = [:]
mainBranchConfig = [:]
repositoriesToBranch = []

pipeline {
    agent {
        docker {
            image env.AGENT_DOCKER_BUILDER_IMAGE
            args env.AGENT_DOCKER_BUILDER_ARGS
            label util.avoidFaultyNodes('ubuntu')
        }
    }

    environment {
        CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")
        PR_BRANCH_HASH = "${util.generateHash(10)}"
    }

    stages {
        stage('Initialize') {
            steps {
                script {
                    checkout scm

                    String projectListStr = getEcosystemProjects().collect { "${getRepoNameCamelCase(it.name)} ${getVersionFromRepository(it.name)}" }.join(' / ')
                    currentBuild.displayName = "Cut-Off ${projectListStr}"

                    sendNotification("Cut-off for ${projectListStr} has started...\n=> ${env.BUILD_URL}")
                }
            }
        }

        stage('Create release branches') {
            steps {
                script {
                    List repositories = getRepositoriesToBranch()
                    createBranches(repositories)

                    sendNotification("Branches have been created for repositories: ${repositories.collect { "${it.name}:${it.branch}" }.join(', ')}")
                }
            }
        }

        stage('Setup branch seed config') {
            steps {
                script {
                    String branchConfigFile = getMainBranchConfigFilePath()
                    String newBranch = getBranchConfigReleaseBranch()
                    def repositories = getRepositoriesToBranch()

                    dir(checkoutMainBranchSeedConfigFileRepo()) {
                        getOrCreateGitBranch(newBranch, getMainBranchConfigFileGitAuthorPushCredentialsId())

                        // Update branch config
                        def branchConfig = readYaml file: branchConfigFile
                        branchConfig.repositories = repositories

                        // Disable some environments when branching as they make no sense by default
                        branchConfig.environments.each { envName, envData ->
                            if (envName.startsWith('quarkus')) {
                                if (envName != 'quarkus-branch') {
                                    envData.enabled = false // Keep only quarkus-branch
                                }
                            }
                        }

                        echo "Write dsl branch_config => ${branchConfig}"
                        sh "rm -f ${branchConfigFile}"
                        writeYaml file: "${branchConfigFile}", data: branchConfig, overwrite: true

                        if (githubscm.isThereAnyChanges()) {
                            sh 'git diff'
                            githubscm.commitChanges('Setup DSL branch config after cut-off')
                            githubscm.pushObject('origin', newBranch, getMainBranchConfigFileGitAuthorPushCredentialsId())

                            sendNotification("Branch config on ${newBranch} has been updated with correct configuration")
                        } else {
                            println '[WARN] No changes to commit'
                        }
                    }
                }
            }
        }

        stage('Branch seed repository') {
            steps {
                script {
                    // Create seed branch if not already exist
                    dir(checkoutSeedRepo()) {
                        getOrCreateGitBranch(getSeedRepoReleaseBranch(), "${SEED_PUSH_CREDENTIALS_ID}")
                    }
                }
            }
        }

        stage('Update main seed config') {
            steps {
                script {
                    String newBranch = getBranchConfigReleaseBranch()
                    String seedConfigFile = env.SEED_CONFIG_FILE_PATH

                    dir(checkoutMainSeedConfigFileRepo()) {
                        def seedConfig = readYaml file: seedConfigFile

                        if (!seedConfig.git.branches.find { it.name == getEcosystemReleaseBranch() }) {
                            def branchConfig = [
                                name: getEcosystemReleaseBranch(),
                                seed: [
                                    branch: getSeedRepoReleaseBranch(),
                                ]
                            ]
                            if (newBranch != getEcosystemReleaseBranch()) {
                                branchConfig.seed.config_file = [
                                    git: [
                                        branch: newBranch
                                    ]
                                ]
                            }
                            seedConfig.git.branches.add(branchConfig)
                        }

                        echo "Write seed config => ${seedConfig}"
                        sh "rm ${seedConfigFile}"
                        writeYaml file: seedConfigFile, data: seedConfig, overwrite: true

                        if (githubscm.isThereAnyChanges()) {
                            String prBranch = getPRBranch(getEcosystemReleaseBranch())

                            githubscm.createBranch(prBranch)
                            sh 'git diff'
                            String commitMsg = "Add release branch ${getEcosystemReleaseBranch()} after cut-off"
                            githubscm.setUserConfigFromCreds(env.SEED_CONFIG_FILE_GIT_AUTHOR_PUSH_CREDS_ID)
                            githubscm.commitChanges(commitMsg)
                            githubscm.pushObject('origin', prBranch, env.SEED_CONFIG_FILE_GIT_AUTHOR_PUSH_CREDS_ID)
                            // Create PR
                            def prBody = "Generated by ${BUILD_URL}"
                            // raise a PR by another user to allow approval by the robot user which is a committer
                            String prLink = githubscm.createPR(commitMsg, prBody, env.SEED_CONFIG_FILE_GIT_BRANCH, env.SEED_CONFIG_FILE_GIT_AUTHOR_CREDS_ID)
                            approveAndMergePR(prLink)
                            sendNotification("Main config has been updated with release branch ${getEcosystemReleaseBranch()}")
                        } else {
                            println '[WARN] No changes to commit'
                        }
                    }
                }
            }
        }

        stage('Call setup jobs') {
            parallel {
                stage('Call init job on new branch') {
                    steps {
                        script {
                            String newBranch = getEcosystemReleaseBranch()
                            sendNotification("Waiting for setup branch job on newly created release branch '${newBranch}' and calling it once available.\n**Please wait for the job to be successful before performing any action on the release branch...**")
                            List buildParams = []
                            def job = [:]
                            try {
                                // Retry 20 times every 5 min
                                retry(20) {
                                    sleep 300
                                    echo "Call job './${newBranch}/setup-branch/0-setup-branch' with buildParams ${buildParams}"
                                    job = build(job: "./${newBranch}/setup-branch/0-setup-branch", wait: true, parameters: buildParams, propagate: false)
                                }
                                if(job.result != 'SUCCESS') {
                                    error "Job ended with unsuccessful result. Please review..."
                                }
                                sendNotification("Setup branch job on newly created release branch '${newBranch}' finished successfully. ")
                            } catch (err) {
                                unstable("Error executing the ${newBranch} setup job ...")
                                String msg = "ERROR: Setup branch job on newly created release branch '${newBranch}' was unsuccessful."
                                if(job) {
                                    msg += "\nPlease review job ${job.absoluteUrl}..."
                                }
                                sendNotification(msg)
                            }
                        }
                    }
                }
            }
        }
    }
    post {
        unsuccessful {
            script {
                sendNotification("${getEcosystemMainProject()}: Cut-off Pipeline job #${BUILD_NUMBER} was: ${currentBuild.currentResult}\nPlease look here: ${BUILD_URL}")
            }
        }
        success {
            script {
                sendNotification("${getEcosystemMainProject()}: Cut-off Pipeline job #${BUILD_NUMBER} was: ${currentBuild.currentResult}")
            }
        }
    }
}

List getRepositoriesToBranch() {
    if (!repositoriesToBranch) {
        repositoriesToBranch = readMainBranchConfig().repositories.findAll { getProjectFromRepository(it.name) }
        def branches = readSeedConfig().git.branches
        if (!branches) {
            error "No branches are defined in main configuration..."
        }
        def mainBranch = branches.find { it.main_branch }?.name ?: ''
        if (!mainBranch) {
            error "No main branch defined into the main configuration..."
        }
        repositoriesToBranch.each { repoConfig ->
            repoConfig.branch = repoConfig.branch ?: mainBranch

            if (repoConfig.name == getMainBranchConfigFileGitRepository()) {
                String gitAuthor = repoConfig.author?.name ?: readMainBranchConfig().git.author.name
                if (gitAuthor == getMainBranchConfigFileGitAuthorName() &&
                    repoConfig.branch == getMainBranchConfigFileGitBranch()) {
                    // we are already going to branch it with the setup of the branch config
                    echo "Repository ${repoConfig.name} is branch config repository"
                    repoConfig.is_branch_config_repo = true
                }
            }
        }
    }
    return repositoriesToBranch
}

void createBranches(List repositories) {
    repositories.each { repoConfig ->
        String repoName = repoConfig.name
        String baseBranch = repoConfig.branch
        String releaseBranch = getReleaseBranchFromRepository(repoConfig.name)
        String gitAuthorCredsId = repoConfig.author?.credentials_id ?: readMainBranchConfig().git.author.credentials_id
        assert gitAuthorCredsId : "Cannot find any credentials Id for git author ${repoConfig.author.name}. Please check your branch seed config file."
        String gitAuthorPushCredsId = repoConfig.author?.push?.credentials_id ?: readMainBranchConfig().git.author.push.credentials_id
        assert gitAuthorPushCredsId : "Cannot find any push credentials Id for git author ${repoConfig.author.name}. Please check your branch seed config file."
        String gitAuthor = repoConfig.author?.name ?: readMainBranchConfig().git.author.name

        echo "Checkout repo ${gitAuthor}/${repoName} on branch ${baseBranch}"
        dir(repoName) {
            deleteDir()
            checkout(githubscm.resolveRepository(repoName, gitAuthor, baseBranch, false, gitAuthorCredsId))
            getOrCreateGitBranch(releaseBranch, gitAuthorPushCredsId)
        }
        // Update the branch with the new release branch
        repoConfig.branch = releaseBranch
    }
}

void getOrCreateGitBranch(String branch, String credentialsId) {
    sh 'git fetch origin'
    String branchRemoteResult = sh(script: "git ls-remote origin ${branch} | wc -l", returnStdout: true).trim()
    if (Integer.parseInt(branchRemoteResult) > 0) {
        echo "Branch ${branch} already exist ... will not create it. Checking out !"
        sh "git checkout ${branch}"
    } else {
        echo "Branch ${branch} does not exist ... gonna create it"
        githubscm.createBranch(branch)
        githubscm.pushObject('origin', branch,  credentialsId)
    }
}

String getPRBranch(String newConfigEntry) {
    return "configure-branch-${newConfigEntry}-${env.PR_BRANCH_HASH}"
}

void approveAndMergePR(String prLink) {
    if (prLink?.trim()) {
        githubscm.approvePR(prLink, env.SEED_CONFIG_FILE_GIT_AUTHOR_PUSH_CREDS_ID)
        githubscm.mergePR(prLink, env.SEED_CONFIG_FILE_GIT_AUTHOR_PUSH_CREDS_ID)
    }
}

void sendNotification(String body) {
    echo 'Send Notification'
    echo body
    emailext body: body, subject: "[${getEcosystemReleaseBranch()}] ${getEcosystemMainProject()}",
                to: env.CI_EMAIL_TO
}

String getProjectFromRepository(String repository) {
    return getEcosystemProjects().find { project -> project.regexs?.any { Pattern.compile(it).matcher(repository).matches() } }?.name
}

String getVersionFromRepository(String repository) {
    def projectName = getProjectFromRepository(repository)
    if (projectName) {
        return getVersionFromProject(projectName)
    } else {
        error "Cannot retrieve repository from repository name: ${repository}. Make sure it matches pattern from ecosystem projects in the main config"
    }
}

String getVersionFromProject(String projectName) {
    String key = getVersionParameterKey(projectName)
    assert params."${key}" : "No parameter ${key} available for project ${projectName}. Please check job configuration."
    return params."${key}"
}

String getVersionParameterKey(String projectName) {
    return "${projectName}_VERSION".toUpperCase()
}

String getReleaseBranchFromRepository(String repository) {
    return util.getReleaseBranchFromVersion(getVersionFromRepository(repository))
}

String getReleaseBranchFromProject(String projectName) {
    return util.getReleaseBranchFromVersion(getVersionFromProject(projectName))
}

String getBranchConfigReleaseBranch() {
    // Check if repository already branched
    return (getRepositoriesToBranch().find { it -> it.is_branch_config_repo }?.branch) ?: "${getEcosystemMainProject()}-${getEcosystemReleaseBranch()}"
}

String getSeedRepoReleaseBranch() {
    return "seed-${getEcosystemMainProject()}-${getEcosystemReleaseBranch()}"
}

def readSeedConfig() {
    assert env.SEED_CONFIG_FILE_PATH : 'Missing main seed config file Path into the environment. Please check the job configuration.'
    if (!seedConfig) {
        dir(checkoutMainSeedConfigFileRepo()) {
            seedConfig = readYaml(file: "${env.SEED_CONFIG_FILE_PATH}")
        }
    }
    return seedConfig
}

String checkoutMainSeedConfigFileRepo() {
    assert env.SEED_CONFIG_FILE_GIT_REPOSITORY : 'Missing main seed config file Git Repository into the environment. Please check the job configuration.'
    assert env.SEED_CONFIG_FILE_GIT_AUTHOR_NAME : 'Missing main seed config file Git Author Name into the environment. Please check the job configuration.'
    assert env.SEED_CONFIG_FILE_GIT_AUTHOR_CREDS_ID : 'Missing main seed config file Git Author Credentials ID into the environment. Please check the job configuration.'
    assert env.SEED_CONFIG_FILE_GIT_BRANCH : 'Missing main seed config file Git Branch into the environment. Please check the job configuration.'

    String dirName = "seed-main-${env.SEED_CONFIG_FILE_GIT_REPOSITORY}"
    dir(dirName) {
        deleteDir()
        checkout(githubscm.resolveRepository(env.SEED_CONFIG_FILE_GIT_REPOSITORY, env.SEED_CONFIG_FILE_GIT_AUTHOR_NAME, env.SEED_CONFIG_FILE_GIT_BRANCH, false, env.SEED_CONFIG_FILE_GIT_AUTHOR_CREDS_ID))
        sh "git checkout ${env.SEED_CONFIG_FILE_GIT_BRANCH}"
    }
    return dirName
}

def readMainBranchConfig() {
    if (!mainBranchConfig) {
        dir(checkoutMainBranchSeedConfigFileRepo()) {
            mainBranchConfig = readYaml(file: "${getMainBranchConfigFilePath()}")
        }
    }
    return mainBranchConfig
}

String checkoutMainBranchSeedConfigFileRepo() {
    String dirName = "seed-branch-${getMainBranchConfigFileGitRepository()}"
    dir(dirName) {
        deleteDir()
        checkout(githubscm.resolveRepository(getMainBranchConfigFileGitRepository(), getMainBranchConfigFileGitAuthorName(), getMainBranchConfigFileGitBranch(), false, getMainBranchConfigFileGitAuthorCredentialsId()))
        sh "git checkout ${getMainBranchConfigFileGitBranch()}"
    }
    return dirName
}

String checkoutSeedRepo() {
    assert env.SEED_REPO : 'Missing seed repo. Please check the job configuration.'
    assert env.SEED_AUTHOR : 'Missing seed author. Please check the job configuration.'
    assert env.SEED_CREDENTIALS_ID : 'Missing seed author credentials ID. Please check the job configuration.'
    assert env.SEED_BRANCH : 'Missing seed branch. Please check the job configuration.'

    String dirName = "seed-repo-${env.SEED_REPO}"
    dir(dirName) {
        deleteDir()
        checkout(githubscm.resolveRepository(env.SEED_REPO, env.SEED_AUTHOR, env.SEED_BRANCH, false, env.SEED_CREDENTIALS_ID))
        sh "git checkout ${env.SEED_BRANCH}"
    }
    return dirName
}

String getMainBranchConfigFileGitRepository() {
    return readSeedConfig().seed.config_file.git.repository
}

String getMainBranchConfigFileGitAuthorName() {
    return readSeedConfig().seed.config_file.git.author.name
}

String getMainBranchConfigFileGitAuthorCredentialsId() {
    return readSeedConfig().seed.config_file.git.author.credentials_id
}

String getMainBranchConfigFileGitAuthorPushCredentialsId() {
    return readSeedConfig().seed.config_file.git.author.push.credentials_id
}

String getMainBranchConfigFileGitBranch() {
    return readSeedConfig().seed.config_file.git.branch
}

String getMainBranchConfigFilePath() {
    return readSeedConfig().seed.config_file.path
}

List getEcosystemProjects() {
    return readSeedConfig().ecosystem.projects.findAll { ! (it.ignore_release ? it.ignore_release.toBoolean() : false)  }
}

List getEcosystemDependencyProjects() {
    return readSeedConfig().ecosystem.dependencies ?: []
}

String getEcosystemMainProject() {
    return readSeedConfig().ecosystem.main_project
}

String getEcosystemReleaseBranch() {
    return getReleaseBranchFromProject(getEcosystemMainProject())
}

String getRepoNameCamelCase(String repo) {
    List words = repo.split('-') as List
    return words.collect { it.isEmpty() ? it : it.substring(0, 1).toUpperCase() + it.substring(1).toLowerCase() }.join(' ')
}
