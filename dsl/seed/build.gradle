apply plugin: 'groovy'

sourceSets {
    jobs {
        groovy {
            srcDirs 'jobs'
            compileClasspath += main.compileClasspath
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.jenkins-ci.org/public/'
    }
    maven {
        url 'https://repo.spring.io/plugins-release/'
    }
}

configurations {
    testPlugins {}
}

// Exclude buggy Xalan dependency this way the JRE default TransformerFactory is used
// The xalan pulled in by htmlunit does not properly deal with spaces folder / job names
configurations.all*.exclude group: 'xalan'
// Some bogus artifact which dissappeared from the remote repos. Not needed anyway.
configurations.all*.exclude group: 'org.netbeans.modules', module: 'org-netbeans-insane'

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.5.13'
    compile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"

    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
    testCompile 'cglib:cglib-nodep:3.3.0' // used by Spock

    testCompile 'org.yaml:snakeyaml:1.21' // used by TestUtil

    // Jenkins test harness dependencies
    testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.66'
    testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
    //testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

    // Job DSL plugin including plugin dependencies
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
    testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
    testCompile 'org.jenkins-ci.plugins:structs:1.22@jar'
    testCompile "org.jenkins-ci.plugins:config-file-provider:3.7.0@jar"
    testCompile 'org.jenkins-ci.plugins:credentials:2.3.17@jar'
    testCompile 'org.jenkins-ci.plugins:cloudbees-folder:6.15@jar'
    testCompile 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.6@jar'

// plugins to install in test instance (all plugins used by the DSL scripts needs to be specified)
    // keep the alphabetical order
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-aggregator:2.6'
    testPlugins 'org.jenkins-ci.plugins.workflow:workflow-step-api:2.22'
    testPlugins 'org.jenkins-ci.plugins:cloudbees-folder:6.15'
    testPlugins 'com.cloudbees.plugins:build-flow-plugin:0.20'
    testPlugins 'com.coravy.hudson.plugins.github:github:1.33.1'
    testPlugins 'com.sonyericsson.hudson.plugins.rebuild:rebuild:1.32'
    testPlugins 'com.synopsys.jenkinsci:ownership:0.13.0'
    testPlugins 'org.jenkins-ci.main:maven-plugin:3.10'
    testPlugins 'org.jenkins-ci.plugins:ansicolor:0.7.0'
    testPlugins 'org.jenkins-ci.plugins:build-timeout:1.20'
    testPlugins 'org.jenkins-ci.plugins:config-file-provider:3.7.0'
    testPlugins 'org.jenkins-ci.plugins:credentials:2.1.17'
    testPlugins 'org.jenkins-ci.plugins:description-setter:1.10'
    testPlugins 'org.jenkins-ci.plugins:email-ext:2.60'
    testPlugins 'org.jenkins-ci.plugins:envinject:2.4.0'
    testPlugins 'org.jenkins-ci.plugins:git:4.7.1'
    testPlugins 'org.jenkins-ci.plugins:ghprb:1.42.2'
    testPlugins 'org.jenkins-ci.plugins:jms-messaging:1.1.19'
    testPlugins 'org.jenkins-ci.plugins:junit:1.49'
    testPlugins 'org.jenkins-ci.plugins:mask-passwords:3.0'
    testPlugins 'org.jenkins-ci.plugins:matrix-project:1.18'
    testPlugins 'org.jenkins-ci.plugins:multiple-scms:0.6'
    testPlugins 'org.jenkins-ci.plugins:nested-view:1.14'
    testPlugins 'org.jenkins-ci.plugins:parameterized-trigger:2.40'
    testPlugins 'org.jenkins-ci.plugins:pipeline-build-step:2.13'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-declarative-agent:1.1.1'
    testPlugins 'org.jenkins-ci.plugins:pipeline-input-step:2.12'
    testPlugins 'org.jenkins-ci.plugins:pipeline-maven:3.10.0'
    testPlugins 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-rest-api:2.19'
    testPlugins 'org.jenkins-ci.plugins:pipeline-stage-step:2.5'
    testPlugins 'org.jenkins-ci.plugins:pipeline-utility-steps:2.7.1'
    testPlugins 'org.jenkins-ci.plugins:pipeline-graph-analysis:1.10'
    testPlugins 'org.jenkins-ci.plugins:pipeline-milestone-step:1.3.2'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-api:1.8.4'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-definition:1.8.4'
    testPlugins 'org.jenkinsci.plugins:pipeline-model-extensions:1.8.4'
    testPlugins 'org.jenkins-ci.plugins.pipeline-stage-view:pipeline-rest-api:2.11'
    testPlugins 'org.jenkins-ci.plugins:script-security:1.76'
    testPlugins 'org.jenkins-ci.plugins:throttle-concurrents:2.2'
    testPlugins 'org.jenkins-ci.plugins:timestamper:1.12'
    testPlugins 'org.jenkins-ci.plugins:token-macro:2.15'
    testPlugins 'org.jenkins-ci.plugins:toolenv:1.2'
    testPlugins 'org.jenkins-ci.plugins:ws-cleanup:0.39'
    testPlugins 'org.jenkins-ci.plugins:xvnc:1.25'
    testPlugins 'org.jenkins-ci.plugins:hidden-parameter:0.0.4'
    testPlugins 'org.jvnet.hudson.plugins:ircbot:2.27'
    // // should be 1.5.11 but 1.5.11 is missing pom in local repo
    // testPlugins 'com.redhat.jenkins.plugins:redhat-ci-plugin:1.5.10'
    testPlugins 'io.jenkins.plugins:warnings-ng:9.0.1'
}

task resolveTestPlugins(type: Copy) {
    from configurations.testPlugins
    into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
    include '*.hpi'
    include '*.jpi'
    def mapping = [:]

    doFirst {
        configurations.testPlugins.resolvedConfiguration.resolvedArtifacts.each {
            mapping[it.file.name] = "${it.name}.${it.extension}"
        }
    }
    rename { mapping[it] }

    doLast {
        List<String> baseNames = source*.name.collect { mapping[it] }.collect { it[0..it.lastIndexOf('.') - 1] }
        new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
    }
}

test {
    dependsOn tasks.resolveTestPlugins
    inputs.files sourceSets.jobs.groovy.srcDirs

    // set build directory for Jenkins test harness, JENKINS-26331
    systemProperty 'buildDirectory', project.buildDir.absolutePath
    testLogging {
        // Show that tests are run in the command-line output
        events 'started', 'passed'
    }
}

task rest(dependsOn: 'classes', type: JavaExec) {
    main = 'com.dslexample.rest.RestApiScriptRunner'
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
}

task wrapperTask(type: Wrapper) {
    gradleVersion = '6.3'
    distributionType = 'all'
}
