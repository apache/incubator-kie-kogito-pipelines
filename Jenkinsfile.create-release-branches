@Library('jenkins-pipeline-shared-libraries')_

KOGITO_REPOS = [
    'kogito-runtimes',
    'kogito-apps',
    'kogito-examples',
    'kogito-images',
    'kogito-cloud-operator',
    'kogito-pipelines' // Create branch to activate nightlies if needed
]

pipeline {
    agent {
        label 'kie-rhel7 && !master'
    }

    parameters {
        string(name: 'DISPLAY_NAME', defaultValue: '', description: 'Setup a specific build display name')

        string(name: 'RELEASE_BRANCH', defaultValue: '', description: 'Release branch to create')
        string(name: 'GIT_AUTHOR', defaultValue: 'kiegroup', description: 'Which Git author repository ?')
        string(name: 'GIT_AUTHOR_CREDS_ID', defaultValue: 'kie-ci', description: 'Git author credentials id')
    }

    stages {
        stage('Initialize') {
            steps {
                script {
                    echo 'Initialize'

                    if (params.DISPLAY_NAME != '') {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    assert getReleaseBranch() != ''
                }
            }
        }

        stage('Create release branches') {
            steps {
                script {
                    echo 'Create release branches'

                    for(String repo : KOGITO_REPOS){
                        echo "Checkout repo ${repo}"
                        dir(repo){
                            deleteDir()
                            checkout(githubscm.resolveRepository(repo, getGitAuthor(), 'master', false))
                            sh 'git fetch origin'
                            String branchRemoteResult = sh(script: "git ls-remote origin ${getReleaseBranch()} | wc -l", returnStdout: true).trim()
                            if(Integer.parseInt(branchRemoteResult) > 0){
                                echo 'Release branch already exist ... will not create it'
                            } else {
                                echo "Release branch ${getReleaseBranch()} does not exist ... gonna create it"
                                githubscm.createBranch(getReleaseBranch())
                                githubscm.pushObject('origin', getReleaseBranch(), getGitAuthorCredsId())
                            }
                        }
                    }
                }
            }
        }
    }
    post {
        always {
            cleanWs()
        }
    }
}

String getReleaseBranch(){
    return params.RELEASE_BRANCH
}

String getGitAuthor(){
    return params.GIT_AUTHOR
}

String getGitAuthorCredsId(){
    return params.GIT_AUTHOR_CREDS_ID
}