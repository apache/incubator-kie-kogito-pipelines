@Library('jenkins-pipeline-shared-libraries')_

pipeline {
    agent {
        label getAgentLabel()
    }
    tools {
        nodejs 'nodejs-12.16.3'
    }
    options {
        timestamps ()
        timeout(time: getTimeoutValue(), unit: 'MINUTES')
    }
    environment {
        FIREFOX_FOLDER = '/opt/tools/firefox-60esr'
    }
    stages {
        stage('Initialize') {
            steps {
                script {
                    sh 'printenv > env_props'
                    archiveArtifacts artifacts: 'env_props'
                }
            }
        }
        stage('check space before build') {
            steps {
                script {
                    util.spaceLeft()
                }
            }
        }
        stage('Install build-chain tool') {
            steps {
                script {
                    println '[INFO] Getting build-chain version from composite action file'
                    def buildChainVersion = buildChain.getBuildChainVersionFromCompositeActionFile()
                    if ([null, 'null'].contains(buildChainVersion)) {
                        def errorMessage = "[ERROR] The build-chain version can't be recovered. Please contact administrator"
                        println errorMessage
                        error(errorMessage)
                    }
                    println "[INFO] build-chain version recovered '${buildChainVersion}'"
                    sh "npm install -g @kie/build-chain-action@${buildChainVersion} -registry=${NPM_REGISTRY_URL}"
                }
            }
        }
        stage('Build projects') {
            tools {
                jdk getJdkTool()
                maven getMavenTool()
            }
            steps {
                script {
                    env.BUILD_MVN_OPTS_CURRENT = env.BUILD_MVN_OPTS_CURRENT ?: ''
                    env.BUILD_MVN_OPTS_CURRENT += isSonarCloudAnalysis() ? ' -Prun-code-coverage' : ''

                    def buildChainActionInfo = getBuildChainActionInfo()

                    configFileProvider([configFile(fileId: getSettingsXmlId(), variable: 'MAVEN_SETTINGS_FILE')]) {
                        withCredentials([string(credentialsId: 'kie-ci1-token', variable: 'GITHUB_TOKEN')]) {
                            sh "build-chain-action -token=${GITHUB_TOKEN} -df='https://raw.githubusercontent.com/kiegroup/kogito-pipelines/${getBuildChainConfigBranch()}/.ci/${buildChainActionInfo.file}' -folder='bc' build ${buildChainActionInfo.action} -url=${env.ghprbPullLink} ${buildChainActionInfo.arguments} --skipParallelCheckout -cct '(^mvn .*)||\$1 -s ${MAVEN_SETTINGS_FILE} -Dmaven.wagon.http.ssl.insecure=true -Dmaven.test.failure.ignore=true'"
                        }
                    }
                }
            }
            post {
                always {
                    junit '**/target/surefire-reports/**/*.xml, **/target/failsafe-reports/**/*.xml'
                    archiveArtifacts artifacts: '**/*.log,**/cypress/screenshots/**,**/cypress/videos/**', allowEmptyArchive: true
                }
                unsuccessful {
                    script {
                        util.archiveConsoleLog('', 300)
                    }
                }
            }
        }
        stage('Sonar analysis') {
            tools {
                jdk getJdkTool()
                maven getMavenTool()
            }
            steps {
                script {
                    if (isSonarCloudAnalysis()) {
                        def project = (env.BUILDCHAIN_PROJECT ? util.getProjectGroupName(env.BUILDCHAIN_PROJECT) : util.getProjectTriggeringJob())[1]
                        // Project should define a `clone` for the current command so that the pom can be found for sonarcloud analysis
                        // Else this next step will fail
                        dir("bc/kiegroup_${project.replaceAll('-', '_')}/${project}") {
                            maven.runMavenWithSettingsSonar(getSettingsXmlId(), "-e -nsu validate -Psonarcloud-analysis -Denforcer.skip=true ${env.SONARCLOUD_ANALYSIS_MVN_OPTS ?: ''}", 'SONARCLOUD_TOKEN', 'sonar_analysis.maven.log')
                        }
                    } else {
                        println '[INFO] No sonar analysis execution.'
                    }
                }
            }
        }
        stage('check space after build') {
            steps {
                script {
                    util.spaceLeft()
                }
            }
        }
    }
    post {
        unsuccessful {
            script {
                if (isPR()) {
                    pullrequest.postComment(util.getMarkdownTestSummary(getNotificationName(), '', "${BUILD_URL}", 'GITHUB'))
                }
            }
        }
        cleanup {
            script {
                // Clean also docker in case of usage of testcontainers lib
                util.cleanNode('docker')
            }
        }
    }
}

String getAgentLabel() {
    return "${env.ADDITIONAL_LABEL?.trim() ? ADDITIONAL_LABEL : 'kie-rhel7 && kie-mem16g'} && !master"
}

String getTimeoutValue() {
    return "${env.ADDITIONAL_TIMEOUT?.trim() ?: 180}"
}

String getJdkTool() {
    return env.BUILD_JDK_TOOL?.trim() ?: 'kie-jdk11'
}

String getMavenTool() {
    return env.BUILD_MAVEN_TOOL?.trim() ?: 'kie-maven-3.8.1'
}

String getPrType() {
    return env.BUILDCHAIN_PR_TYPE?.trim() ?: 'pr'
}

boolean isPR() {
    return getPrType() == 'pr'
}

boolean isFDB() {
    return getPrType() == 'fdb'
}

boolean isSingle() {
    return getPrType() == 'single'
}

def getBuildChainActionInfo() {
    def definitionFile = isLts() ? 'pull-request-lts-config.yaml' : 'pull-request-config.yaml'
    return isFDB() ? [action: 'fd', file: definitionFile, arguments: getBuildChainAdditionalArguments()] :
                     isSingle() ? [action: 'single', file: definitionFile, arguments: getBuildChainAdditionalArguments()] :
                     [action: 'pr', file: definitionFile, arguments: getBuildChainAdditionalArguments()]
}

def getBuildChainAdditionalArguments() {
    return env.BUILDCHAIN_PROJECT ? "-sp=${env.BUILDCHAIN_PROJECT.trim()}" : ''
}

String getBuildChainConfigBranch() {
    return env.BUILDCHAIN_CONFIG_BRANCH ?: '\${BRANCH:main}'
}

String getSettingsXmlId() {
    return isPR() || isFDB() ? 'kogito_pr_settings' : 'kogito_release_settings'
}

boolean isLts() {
    return env.LTS ? env.LTS.toBoolean() : false
}

boolean isSonarCloudAnalysis() {
    if (env.DISABLE_SONARCLOUD?.toBoolean()) {
        return false
    }
    if (isPR()) {
        return !env.DOWNSTREAM_BUILD?.toBoolean()
    }
    return false
}

String getNotificationName() {
    return env.NOTIFICATION_JOB_NAME ?: 'PR'
}