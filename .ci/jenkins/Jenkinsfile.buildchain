@Library('jenkins-pipeline-shared-libraries')_

pipeline {
    agent {
        label getAgentLabel()
    }
    tools {
        nodejs 'nodejs-12.16.3'
    }
    options {
        timestamps ()
        timeout(time: getTimeoutValue(), unit: 'MINUTES')
    }
    environment {
        FIREFOX_FOLDER = '/opt/tools/firefox-60esr'
    }
    stages {
        stage('Initialize') {
            steps {
                sh 'printenv'
            }
        }
        // executes a script that compresses the consoleText and attaches it to the mail
        stage('build sh script') {
            steps {
                script {
                    mailer.buildLogScriptPR()
                }
            }
        }
        stage('check space before build') {
            steps {
                script {
                    util.spaceLeft()
                }
            }
        }
        stage('Build projects') {
            tools {
                jdk getJdkTool()
                maven getMavenTool()
            }
            steps {
                script {
                    if (isSonarCloudAnalysis()) {
                        env.BUILD_MVN_OPTS_CURRENT = "${env.BUILD_MVN_OPTS_CURRENT ?: ''} -Prun-code-coverage"
                    }
                    def buildChainActionInfo = getBuildChainActionInfo()

                    configFileProvider([configFile(fileId: getSettingsXmlId(), variable: 'MAVEN_SETTINGS_FILE')]) {
                        withCredentials([string(credentialsId: 'kie-ci1-token', variable: 'GITHUB_TOKEN')]) {
                            sh "build-chain-action -token=${GITHUB_TOKEN} -df='https://raw.githubusercontent.com/kiegroup/kogito-pipelines/${getBuildChainConfigBranch()}/.ci/${buildChainActionInfo.file}' -folder='bc' build ${buildChainActionInfo.action} ${buildChainActionInfo.arguments} --skipParallelCheckout -cct '(^mvn .*)||\$1 -s ${MAVEN_SETTINGS_FILE} -Dmaven.wagon.http.ssl.insecure=true'"
                        }
                    }
                }
            }
        }
        stage('Sonar analysis') {
            tools {
                jdk getJdkTool()
                maven getMavenTool()
            }
            steps {
                script {
                    if (isSonarCloudAnalysis()) {
                        def project = (env.BUILDCHAIN_PROJECT ? util.getProjectGroupName(env.BUILDCHAIN_PROJECT) : util.getProjectTriggeringJob())[1]
                        dir("bc/kiegroup_${project.replaceAll('-', '_')}/${project}") {
                            maven.runMavenWithSettingsSonar(getSettingsXmlId(), "-e -nsu validate -Psonarcloud-analysis -Denforcer.skip=true ${env.SONARCLOUD_ANALYSIS_MVN_OPTS ?: ''}", 'SONARCLOUD_TOKEN', 'sonar_analysis.maven.log')
                        }
                    } else {
                        println '[INFO] No sonar analysis execution.'
                    }
                }
            }
        }
        stage('check space after build') {
            steps {
                script {
                    util.spaceLeft()
                }
            }
        }
    }
    post {
        always {
            sh '$WORKSPACE/trace.sh'
            junit '**/target/surefire-reports/**/*.xml, **/target/failsafe-reports/**/*.xml'
            archiveArtifacts(artifacts: '**/cypress/screenshots/**,**/cypress/videos/**', fingerprint: false, allowEmptyArchive: true)
        }
        failure {
            script {
                mailer.sendEmail_failedPR()
            }
        }
        unstable {
            script {
                mailer.sendEmail_unstablePR()
            }
        }
        fixed {
            script {
                mailer.sendEmail_fixedPR()
            }
        }
        cleanup {
            script {
                // Clean also docker in case of usage of testcontainers lib
                util.cleanNode('docker')
            }
        }
    }
}

String getAgentLabel() {
    return "${env.ADDITIONAL_LABEL?.trim() ? ADDITIONAL_LABEL : 'kie-rhel7 && kie-mem16g'} && !master"
}

String getTimeoutValue() {
    return "${env.ADDITIONAL_TIMEOUT?.trim() ?: (isNative() ? 360 : 180)}"
}

String getJdkTool() {
    return env.BUILD_JDK_TOOL?.trim() ?: 'kie-jdk11'
}

String getMavenTool() {
    return env.BUILD_MAVEN_TOOL?.trim() ?: 'kie-maven-3.8.1'
}

String getBuildChainType() {
    return env.BUILDCHAIN_PR_TYPE?.trim() ?: 'pr'
}

boolean isPRBuildChainType() {
    return getBuildChainType() == 'pr'
}

boolean isFDBBuildChainType() {
    return getBuildChainType() == 'fdb'
}

boolean isBranchBuildChainType() {
    return getBuildChainType() == 'branch'
}

def getBuildChainActionInfo() {
    def definitionFile = isNative() ? 'pull-request-native-config.yaml' :
                         isLts() ? 'pull-request-lts-config.yaml' :
                         'pull-request-config.yaml'
    List arguments = []
    if (env.BUILDCHAIN_PROJECT) {
        arguments.add("-sp=${env.BUILDCHAIN_PROJECT.trim()}")
    }
    if (isBranchBuildChainType()) {
        arguments.add("-p=${env.BUILDCHAIN_PROJECT.trim()}")
        arguments.add("-b=${env.BUILDCHAIN_BRANCH.trim()}")
        arguments.add("-g=${env.BUILDCHAIN_AUTHOR.trim()}")
    } else {
        arguments.add("-url=${env.ghprbPullLink} ")
    }

    return [action: getBuildChainType(), file: definitionFile, arguments: getBuildChainAdditionalArguments()]
}

String getBuildChainConfigBranch() {
    return env.BUILDCHAIN_CONFIG_BRANCH ?: 'main'
}

String getSettingsXmlId() {
    return isPRBuildChainType() || isFDBBuildChainType() ? 'kogito_pr_settings' : 'kogito_release_settings'
}

boolean isDefaultCheck() {
    return !isNative() && !isLts()
}

boolean isNative() {
    return env.NATIVE ? env.NATIVE.toBoolean() : false
}

boolean isLts() {
    return env.LTS ? env.LTS.toBoolean() : false
}

boolean isSonarCloudAnalysis() {
    if (env.DISABLE_SONARCLOUD?.toBoolean()) {
        return false
    }
    if (isPRBuildChainType()) {
        return isDefaultCheck() && !env.DOWNSTREAM_BUILD?.toBoolean()
    }
    return false
}