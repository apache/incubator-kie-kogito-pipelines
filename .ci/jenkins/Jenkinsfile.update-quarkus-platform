import org.jenkinsci.plugins.workflow.libs.Library
@Library('jenkins-pipeline-shared-libraries')_

import org.kie.jenkins.MavenCommand

AGENT_LABEL="kie-rhel7 && kie-mem8g && !built-in"
quarkusPlatformRepo="quarkus-platform"
branchCreated = false

pipeline {

    agent {
        label "${AGENT_LABEL}"
    }

    options{
        timestamps()
        timeout(time: 60, unit: 'MINUTES')
    }

    tools {
        maven "${BUILD_MAVEN_TOOL}"
        jdk "${BUILD_JDK_TOOL}"
    }

    environment {
        // Static env is defined into .jenkins/dsl/jobs.groovy file

        KOGITO_CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")
    }

    stages{
        stage('CleanWorkspace') {
            steps {
                cleanWs()
            }
        }
		stage('Initialize') {
			steps{
				script{
                    sh 'printenv'
                    
                    checkout scm

                    dir(quarkusPlatformRepo){
                        checkoutRepo(quarkusPlatformRepo, getQuarkusPlatformAuthor(), getQuarkusPlatformBranch(), getQuarkusPlatformAuthorCredsId())
                        githubscm.forkRepo("${FORK_GIT_AUTHOR_CREDS_ID}")
                    }
				}
			}
		}
        stage('stage') {
			when {
				expression { getCommand() == 'stage' }
			}
            steps {
                script {
                    String prLink = null
                    String commitMsg = getCommitMessage()
                    String localBranch = getLocalBranchName()
                    dir(quarkusPlatformRepo){
                        branchCreated = getOrCreateGitBranch(localBranch, "${FORK_GIT_AUTHOR_CREDS_ID}")
                        
                        // run update-quarkus-platform.sh with 'stage'
                        sh "${WORKSPACE}/tools/update-quarkus-platform.sh -p ${getProjectName()} -s -v ${getNewVersion()} -f ${FORK_GIT_AUTHOR} -h ${localBranch} -n -r ${getCommand()}"
                        
                        sh "git status"

                        // Add changed files, commit and open PR
                        def prBody = "Generated by Kogito pipelines"

                        if (githubscm.isThereAnyChanges()) {
                            githubscm.commitChanges(commitMsg, { sh "git add --all" })
                            githubscm.pushObject('origin', localBranch, "${FORK_GIT_AUTHOR_CREDS_ID}")   
                            if (branchCreated) {
                                prLink = githubscm.createPrAsDraft(commitMsg, prBody, getQuarkusPlatformBranch(), "${FORK_GIT_AUTHOR_CREDS_ID}")
                                sendNotification("Draft PR was created with ${getProjectName()} to version ${getNewVersion()}.\nHere is the PR link: ${prLink}")
                            } else {
                                echo "Branch ${localBranch} was already created so assuming the PR exists alrerady ..."
                                sendNotification("Current PR was updated with ${getProjectName()} to version ${getNewVersion()}.")
                            }
                        } else {
                            println '[WARN] no changes to commit'
                        }
                    }
                }
            }
        }
        stage('finalize') {
			when {
				expression { getCommand() == 'finalize' }
			}
            steps {
                script {
                    String commitMsg = getCommitMessage()
                    String localBranch = getLocalBranchName()
                    dir(quarkusPlatformRepo){
                        sh "git fetch origin"
                        sh "git checkout ${localBranch}"
                        
                        // run update-quarkus-platform.sh with 'finalize'
                        sh "${WORKSPACE}/tools/update-quarkus-platform.sh -p ${getProjectName()} -s -d -v ${getNewVersion()} -f ${FORK_GIT_AUTHOR} -h ${localBranch} -n -r ${getCommand()}"
                        
                        githubscm.squashCommits(getQuarkusPlatformBranch(), commitMsg)

                        githubscm.pushObject('--force-with-lease origin', localBranch, "${FORK_GIT_AUTHOR_CREDS_ID}")
                        sendNotification("PR was finalized with ${getProjectName()} to version ${getNewVersion()}.")
                    }
                }
            }
        }
    }
    post {
        unsuccessful {
            sendErrorNotification()
        }
    }
}

String commitAndCreatePR(String commitMsg, Closure precommit, String localBranch, String targetBranch) {
    def prBody = "Generated by build ${BUILD_TAG}: ${BUILD_URL}"
    githubscm.commitChanges(commitMsg, precommit)
    githubscm.pushObject('origin', localBranch)
    return githubscm.createPrAsDraft(commitMsg, prBody, targetBranch)
}

/**
* Return true if the branch was created
*/
boolean getOrCreateGitBranch(String branch, String credentialsId) {
    sh 'git fetch origin'
    String branchRemoteResult = sh(script: "git ls-remote origin ${branch} | wc -l", returnStdout: true).trim()
    if (Integer.parseInt(branchRemoteResult) > 0) {
        echo "Branch ${branch} already exist ... will not create it. Checking out !"
        sh "git checkout origin/${branch} -b ${branch}"
        return false
    } else {
        echo "Branch ${branch} does not exist ... gonna create it"
        githubscm.createBranch(branch)
        githubscm.pushObject('origin', branch,  credentialsId)
        return true
    }
}

void checkoutRepo(String repo, String author, String branch, String credentialsId) {
    checkout(githubscm.resolveRepository(repo, author, branch, false, credentialsId))
    // need to manually checkout branch since on a detached branch after checkout command
    sh "git checkout ${branch}"
}

void sendNotification(String body) {
    emailext body: "${body}",
             subject: "[${getBuildBranch()}] Quarkus Platform update",
             to: env.KOGITO_CI_EMAIL_TO
}

void sendErrorNotification() {
    sendNotification("Job #${BUILD_NUMBER} was: **${currentBuild.currentResult}**\nPlease look here: ${BUILD_URL}")
}

String getCommitMessage() {
    return "Bump up ${getProjectName()} to ${getNewVersion()}"
}

String getLocalBranchName() {
    return params.PR_BRANCH ?: "bump-${getProjectName()}-${getNewVersion()}"
}

String getNewVersion() {
    return params.NEW_VERSION
}

String getCommand() {
    return params.COMMAND
}

String getProjectName() {
    return env.PROJECT_NAME
}

String getBuildBranch() {
    return env.BUILD_BRANCH_NAME
}

String getGitAuthor() {
    return env.GIT_AUTHOR
}

String getGitAuthorCredsId() {
    return env.GIT_AUTHOR_CREDENTIALS_ID
}

String getQuarkusPlatformBranch() {
    return env.QUARKUS_PLATFORM_BRANCH
}

String getQuarkusPlatformAuthor() {
    return env.QUARKUS_PLATFORM_AUTHOR_NAME
}

String getQuarkusPlatformAuthorCredsId() {
    return env.QUARKUS_PLATFORM_AUTHOR_CREDENTIALS_ID
}
