import org.jenkinsci.plugins.workflow.libs.Library

@Library('jenkins-pipeline-shared-libraries')_

// Map of executed jobs
// See https://javadoc.jenkins.io/plugin/workflow-support/org/jenkinsci/plugins/workflow/support/steps/build/RunWrapper.html
// for more options on built job entity
JOBS = [:]

FAILED_STAGES = [:]
UNSTABLE_STAGES = [:]

defaultImageParamsPrefix = 'IMAGE'
kogitoImagesParamsPrefix = 'KOGITO_IMAGES'
examplesImagesParamsPrefix = 'EXAMPLES_IMAGES'

// Should be multibranch pipeline
pipeline {
    agent {
        label 'kie-rhel8 && !built-in'
    }

    options {
        timeout(time: 360, unit: 'MINUTES')
    }

    // parameters {
    // For parameters, check into ./dsl/jobs.groovy file
    // }

    environment {
        // Some generated env is also defined into ./dsl/jobs.groovy file

        KOGITO_CI_EMAIL_TO = credentials("${JENKINS_EMAIL_CREDS_ID}")

        IMAGE_NAME_NIGHTLY_SUFFIX = 'nightly'

        // Use branch name in nightly tag as we may have parallel main and release branch builds
        NIGHTLY_TAG = """${getBuildBranch()}-${sh(
                returnStdout: true,
                script: 'date -u "+%Y-%m-%d"'
            ).trim()}"""
}

    stages {
        stage('Initialize') {
            steps {
                script {
                    echo "nightly tag is ${env.NIGHTLY_TAG}"

                    currentBuild.displayName = env.NIGHTLY_TAG
                }
            }
        }

        stage('Init Kogito Runtimes') {
            steps {
                script {
                    def buildParams = getDefaultBuildParams()
                    addDroolsVersionParam(buildParams)
                    addKogitoVersionParam(buildParams)
                    buildJob('kogito-runtimes', buildParams)
                }
            }
            post {
                failure {
                    addFailedStage('kogito-runtimes')
                }
            }
        }

        stage('Init Kogito Apps') {
            steps {
                script {
                    def buildParams = getDefaultBuildParams()
                    addKogitoVersionParam(buildParams)
                    buildJob('kogito-apps', buildParams)
                }
            }
            post {
                failure {
                    addFailedStage('kogito-apps')
                }
            }
        }

        stage('Init Kogito Examples') {
            steps {
                script {
                    def buildParams = getDefaultBuildParams()
                    addDroolsVersionParam(buildParams)
                    addKogitoVersionParam(buildParams)
                    buildJob('kogito-examples', buildParams)

                    if (isJobSucceeded('kogito-examples') || isJobUnstable('kogito-examples')) {
                        // Update examples nightly branch
                        dir('kogito-examples') {
                            deleteDir()
                            checkout(githubscm.resolveRepository('kogito-examples', getGitAuthor(), getBuildBranch(), false))
                            String nightlyBranch = "nightly-${getBuildBranch()}"
                            githubscm.createBranch(nightlyBranch)
                            githubscm.pushObject('origin', nightlyBranch, getGitAuthorCredsID())
                        }
                    }
                }
            }
            post {
                failure {
                    addFailedStage('kogito-examples')
                }
            }
        }

        stage('Init Images') {
            steps {
                script {
                    def buildParams = getDefaultBuildParams()
                    addStringParam(buildParams, 'APPS_REF', "${getBuildBranch()}")
                    addStringParam(buildParams, 'APPS_URI', "https://github.com/${getGitAuthor()}/kogito-apps")
                    addImageBuildParams(buildParams, env.NIGHTLY_TAG)
                    addStringParam(buildParams, 'KOGITO_VERSION', getKogitoVersion().toLowerCase())
                    addStringParam(buildParams, 'KOGITO_ARTIFACTS_VERSION', getKogitoVersion())
                    buildJob('kogito-images', buildParams)
                }
            }
            post {
                failure {
                    addFailedStage('kogito-images')
                }
            }
        }

        stage('Init operators') {
            parallel {
                stage('Init Kogito Operator') {
                    stages {
                        stage('Init Examples Images') {
                            steps {
                                script {
                                    def buildParams = getDefaultBuildParams()
                                    addImageBuildParams(buildParams, env.NIGHTLY_TAG)
                                    addStringParam(buildParams, constructKey(defaultImageParamsPrefix, 'NAME_PREFIX'), 'examples')
                                    addImageBuildParams(buildParams, env.NIGHTLY_TAG, kogitoImagesParamsPrefix)
                                    addStringParam(buildParams, 'EXAMPLES_URI', "https://github.com/${getGitAuthor()}/kogito-examples")
                                    addStringParam(buildParams, 'EXAMPLES_REF', "nightly-${getBuildBranch()}")
                                    buildJob('kogito-examples-images', buildParams)
                                }
                            }
                            post {
                                failure {
                                    addFailedStage('kogito-examples-images')
                                }
                            }
                        }
                        stage('Init Operator') {
                            steps {
                                script {
                                    def buildParams = getDefaultBuildParams()
                                    addStringParam(buildParams, 'PROJECT_VERSION', getKogitoVersion().toLowerCase())
                                    addImageBuildParams(buildParams, env.NIGHTLY_TAG)
                                    buildJob('kogito-operator', buildParams)
                                }
                            }
                            post {
                                failure {
                                    addFailedStage('kogito-operator')
                                }
                            }
                        }
                    }
                }

                stage('Init Serverless Operator') {
                    steps {
                        script {
                            def buildParams = getDefaultBuildParams()
                            addStringParam(buildParams, 'PROJECT_VERSION', getKogitoVersion().toLowerCase())
                            addImageBuildParams(buildParams, env.NIGHTLY_TAG)
                            buildJob('kogito-serverless-operator', buildParams)
                        }
                    }
                    post {
                        failure {
                            addFailedStage('kogito-serverless-operator')
                        }
                    }
                }
            }
        }
        
        stage('Init Docs') {
            steps {
                script {
                    def buildParams = getDefaultBuildParams()
                    buildJob('kogito-docs', buildParams)
                }
            }
            post {
                failure {
                    addFailedStage('kogito-docs')
                }
            }
        }

        // Launch the nightly to deploy all artifacts from the branch
        stage('Launch the nightly') {
            steps {
                script {
                    def buildParams = getDefaultBuildParams()
                    addBooleanParam(buildParams, 'SKIP_TESTS', true)
                    buildJob('../nightly/kogito-nightly', buildParams, 'kogito-nightly', false)
                }
            }
            post {
                failure {
                    addFailedStage('kogito-nightly')
                }
            }
        }
    }
    post {
        unsuccessful {
            sendPipelineErrorNotification()
        }
    }
}

def buildJob(String jobName, List buildParams, String jobKey = jobName, boolean waitForJob = true) {
    echo "[${jobKey}] Build ${jobName} with params ${buildParams}"

    def job = build(job: "${jobName}", wait: waitForJob, parameters: buildParams, propagate: false)
    JOBS[jobKey] = job

    // Set Unstable if job did not succeed
    if (!isJobSucceeded(jobKey)) {
        addUnstableStage(jobKey)
        unstable("Job ${jobName} finished with result ${job.result}")
    }
    return job
}

def getJob(String jobKey) {
    return JOBS[jobKey]
}

String getJobUrl(String jobKey) {
    echo "getJobUrl for ${jobKey}"
    return getJob(jobKey)?.absoluteUrl ?: ''
}

boolean isJobSucceeded(String jobKey) {
    return getJob(jobKey)?.result == 'SUCCESS'
}

boolean isJobUnstable(String jobKey) {
    return getJob(jobKey)?.result == 'UNSTABLE'
}

void addFailedStage(String jobKey = '') {
    FAILED_STAGES.put("${STAGE_NAME}", jobKey)
}
void addUnstableStage(String jobKey = '') {
    UNSTABLE_STAGES.put("${STAGE_NAME}", jobKey)
}

void sendPipelineErrorNotification() {
    String bodyMsg = "Kogito setup branch job #${BUILD_NUMBER} was: ${currentBuild.currentResult}"

    if (FAILED_STAGES.size()) {
        bodyMsg += '\nFailed stages: \n- '
        bodyMsg += FAILED_STAGES.collect { "${it.key} => ${getJobUrl(it.value)}" }.join('\n- ')
    }
    bodyMsg += '\n'
    if (UNSTABLE_STAGES.size()) {
        bodyMsg += '\nUnstable stages: \n- '
        bodyMsg += UNSTABLE_STAGES.collect { "${it.key} => ${getJobUrl(it.value)}" }.join('\n- ')
    }
    bodyMsg += '\n'
    bodyMsg += "\nPlease look here: ${BUILD_URL}"
    emailext body: bodyMsg, subject: "[${getBuildBranch()}][d] Setup branch",
                to: env.KOGITO_CI_EMAIL_TO
}

List getDefaultBuildParams() {
    List buildParams = []
    addStringParam(buildParams, 'DISPLAY_NAME', "${env.NIGHTLY_TAG}")
    addBooleanParam(buildParams, 'SEND_NOTIFICATION', true)

    return buildParams
}

void addDroolsVersionParam(buildParams) {
    addStringParam(buildParams, 'DROOLS_VERSION', getDroolsVersion())
}

void addKogitoVersionParam(buildParams) {
    addStringParam(buildParams, 'KOGITO_VERSION', getKogitoVersion())
}

void addImageBuildParams(List buildParams, String tag, String paramsPrefix = defaultImageParamsPrefix, String extraSuffix = '') {
    addStringParam(buildParams, constructKey(paramsPrefix, 'REGISTRY_CREDENTIALS'), env.IMAGE_REGISTRY_CREDENTIALS)
    addStringParam(buildParams, constructKey(paramsPrefix, 'REGISTRY'), env.IMAGE_REGISTRY)
    addStringParam(buildParams, constructKey(paramsPrefix, 'NAMESPACE'), env.IMAGE_NAMESPACE)
    addStringParam(buildParams, constructKey(paramsPrefix, 'NAME_SUFFIX'), (extraSuffix ? "${extraSuffix}-" : '') + env.IMAGE_NAME_NIGHTLY_SUFFIX)
    addStringParam(buildParams, constructKey(paramsPrefix, 'TAG'), tag)
}

void addDeployImageWithLatestTagParam(buildParams) {
    addBooleanParam(buildParams, 'DEPLOY_WITH_LATEST_TAG', isDeployImagesLatestTag())
}

void addStringParam(List params, String key, String value) {
    params.add(string(name: key, value: value))
}

void addBooleanParam(List params, String key, boolean value) {
    params.add(booleanParam(name: key, value: value))
}

String constructKey(String prefix, String paramId) {
    return prefix ? "${prefix}_${paramId}" : paramId
}

String getBuildBranch() {
    return env.GIT_BRANCH_NAME
}

String getGitAuthor() {
    return env.GIT_AUTHOR
}

String getGitAuthorCredsID() {
    return env.GIT_AUTHOR_CREDS_ID
}

String getDroolsVersion() {
    return params.DROOLS_VERSION ?: getVersionFromReleaseBranch(util.calculateTargetReleaseBranch(getBuildBranch(), 7))
}

String getKogitoVersion() {
    return params.KOGITO_VERSION ?: getVersionFromReleaseBranch(getBuildBranch())
}

String getVersionFromReleaseBranch(String releaseBranch, int microVersion = 0, String suffix = 'SNAPSHOT') {
    String [] versionSplit = releaseBranch.split("\\.")
    if (versionSplit.length == 3
        && versionSplit[0].isNumber()
        && versionSplit[1].isNumber()
        && versionSplit[2] == 'x') {
        return "${versionSplit[0]}.${versionSplit[1]}.${microVersion}${suffix ? '-' + suffix : ''}"
    } else {
        error 'Cannot parse given branch as a release branch, aka [M].[m].x ...'
    }
}
